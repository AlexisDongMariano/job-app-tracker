terraform {
    required_version = ">= 1.0"

    required_providers {
        azurerm = {
            source = "hashicorp/azurerm"
            version = "~> 3.0"
        }
    }
}

# Optional: Store state remotely
# backend "azurerm" {
#    resource_group_name = "tfstate-rg"
#    storage_account_name = "tfstatestorage"
#    container_name = "tfstate"
#    key = "test.terraform.tfstate"
# }

provider "azurerm" {
    features {}
}

resource "azurerm_resource_group" "main" {
    name = var.resource_group_name
    location = var.location

    tags = {
        Owner = var.owner_email
    }
}

resource "azurerm_virtual_network" "main" {
    name = "${var.vm_name}-vnet"
    address_space = ["10.0.0.0/16"]
    location = azurerm_resource_group.main.location
    resource_group_name = azurerm_resource_group.main.name
}

resource "azurerm_subnet" "main" {
    name = "${var.vm_name}-subnet"
    resource_group_name = azurerm_resource_group.main.name
    virtual_network_name = azurerm_virtual_network.main.name
    address_prefixes = ["10.0.1.0/24"]
}

resource "azurerm_network_security_group" "main" {
    name = "${var.vm_name}-nsg"
    location = azurerm_resource_group.main.location
    resource_group_name = azurerm_resource_group.main.name
}


resource "azurerm_subnet_network_security_group_association" "main" {
    subnet_id = azurerm_subnet.main.id
    network_security_group_id = azurerm_network_security_group.main.id
}

# NSG Rules
resource "azurerm_network_security_rule" "ssh" {
    name = "SSH"
    priority = 1000
    direction = "Inbound"
    access = "Allow"
    protocol = "Tcp"
    source_port_range = "*"
    destination_port_range = "22"
    source_address_prefix = var.my_public_ip
    destination_address_prefix = "*"
    resource_group_name = azurerm_resource_group.main.name
    network_security_group_name = azurerm_network_security_group.main.name
}

resource "azurerm_network_security_rule" "http" {
    name = "HTTP"
    priority = 1001
    direction = "Inbound"
    access = "Allow"
    protocol = "Tcp"
    source_port_range = "*"
    destination_port_range = "80"
    source_address_prefix = var.my_public_ip
    destination_address_prefix = "*"
    resource_group_name = azurerm_resource_group.main.name
    network_security_group_name = azurerm_network_security_group.main.name
}

resource "azurerm_network_security_rule" "https" {
    name = "HTTPS"
    priority = 1002
    direction = "Inbound"
    access = "Allow"
    protocol = "Tcp"
    source_port_range = "*"
    destination_port_range = "443"
    source_address_prefix = var.my_public_ip
    destination_address_prefix = "*"
    resource_group_name = azurerm_resource_group.main.name
    network_security_group_name = azurerm_network_security_group.main.name
}


resource "azurerm_public_ip" "main" {
    name = "${var.vm_name}-pip"
    location = azurerm_resource_group.main.location
    resource_group_name = azurerm_resource_group.main.name
    allocation_method = "Static"
    sku = "Standard"

    tags = {
        Owner = var.owner_email
    }
}

resource "azurerm_network_interface" "main" {
    name = "${var.vm_name}-nic"
    location = azurerm_resource_group.main.location
    resource_group_name = azurerm_resource_group.main.name

    ip_configuration {
        name = "internal"
        subnet_id = azurerm_subnet.main.id
        private_ip_address_allocation = "Dynamic"
        public_ip_address_id = azurerm_public_ip.main.id
    }

    tags = {
        Owner = var.owner_email
    }
}

# Virtual Machine
resource "azurerm_linux_virtual_machine" "main" {
    name = var.vm_name
    location = azurerm_resource_group.main.location
    resource_group_name = azurerm_resource_group.main.name
    size = var.vm_size
    admin_username = var.admin_username
    network_interface_ids = [azurerm_network_interface.main.id]

    disable_password_authentication = true

    admin_ssh_key {
        username = var.admin_username
        public_key = var.ssh_public_key
    }

    os_disk {
        caching = "ReadWrite"
        storage_account_type = "Standard_LRS"
    }

    source_image_reference {
        publisher = "Canonical"
        offer = "0001-com-ubuntu-server-jammy"
        sku = "22_04-lts-gen2"
        version = "latest"
    }

    tags = {
        Owner = var.owner_email
    }
}

